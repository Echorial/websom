class Websom.Services.Config inherits Websom.Service {
	map optionValues = new map();
	map optionValuesFromDatabase = new map();
	map optionDefaults = new map();

	map colorProfiles = new map();
	map navigation = new map();

	<<string>array>array publicDefaults = new <<string>array>array();
	bool publicValueCache = false;
	map publicValues = new map();

	string global = "";

	// We lazy load options in bulk. All or nothing.
	bool loaded = false;

	// Routes could be:
	// type.package-name.namespace -> theme.theme.icon
	// type.sub-type.package-name -> adapter.database.loki
	// type.package-name -> module.usermanager
	string getString(string route, string option) {
		return this.getPrimitive(route, option);
	}

	primitive getPrimitive(string route, string option) {
		if (this.loaded == false)
			this.load();

		let map optionValuesFromDatabase = this.optionValuesFromDatabase[route];
		let map optionValues = this.optionValues[route];

		if (optionValues != null and optionValues.hasKey(option)) {
			return this.optionValues[route][option];
		}else if (optionValuesFromDatabase != null and optionValuesFromDatabase.hasKey(option)) {
			return this.optionValuesFromDatabase[route][option];
		}else{
			return this.optionDefaults[route + "." + option];
		}
	}

	primitive getPrimitiveNonDefault(string route, string option) {
		if (this.loaded == false)
			this.load();

		let map optionValuesFromDatabase = this.optionValuesFromDatabase[route];
		let map optionValues = this.optionValues[route];

		if (optionValues != null and optionValues.hasKey(option)) {
			return this.optionValues[route][option];
		}else if (optionValuesFromDatabase != null and optionValuesFromDatabase.hasKey(option)) {
			return this.optionValuesFromDatabase[route][option];
		}

		return null;
	}

	`Executed after starting the database service`
	void loadFromDatabase() {
		this.server.api.route("/data", void (Websom.Request req) => {
			req.header("Content-Type", "application/json");
			req.end(Websom.Json.encode(this.computeClientData(req)));
		});
	}

	void load() {
		if (this.server.database.primary != null) {
			// TODO: Load config from database
		}

		// Load all overrides from the file system.
		// These take precedence and "override" options coming from the DB.

		let rawOverrides = Websom.Json.parse(FileSystem.readSync(this.global, "utf8"));
		this.mergeOptions(rawOverrides);
	}

	void mergeOptions(map rawOverrides) {
		for (let string key in rawOverrides) {
			if ((key in this.optionValues) == false)
				this.optionValues[key] = new map();
			
			let rawVal = rawOverrides[key];

			for (let string valKey in rawVal) {
				this.optionValues[key][valKey] = rawVal[valKey];
			}
		}
	}

	override Websom.Status start() {
		this.global = this.server.config.configOverrides + "/global.json";

		native javascript {
			if (this.server.config.dev) {
				const fs = require("fs");

				let throttle = Date.now();

				fs.watch(this.server.config.configOverrides, {recursive: true}, (type, file) => {
					if (file == "optionsCache.json")
						return;
					
					if (throttle > (Date.now() - 2000)) {
						console.log("Throttled " + type + " on " + file);
						
						return;
					}else{
						throttle = Date.now();
					}

					if (this.server.restartHandler)
						this.server.restartHandler();
				});
			}
		}
	}

	// For debugging
	void logOptions() {
		this.load();
		Console.log(this.optionValues);
	}

	void gatherOptions() {
		let cache = this.server.config.configOverrides + "/optionsCache.json";

		if (this.server.config.dev == false and FileSystem.exists(cache)) {
			let rawConfigs = FileSystem.readSync(cache, "utf8");

			let configs = Websom.Json.parse(rawConfigs);

			this.fillDefaults(configs);
		}else{
			if (this.server.config.dev) {
				if (this.server.config.verbose) {
					Console.log("No options cache found. Websom will (re)build it.");
					Console.log("Note: This is always rebuilt while in dev mode.");
				}

				this.fillDefaults(this.cacheOptions());
			}else{
				//Console.log("No options cache found. Websom can't function without it. Try redeploying this production build from a dev environment.");
			}
		}
	}

	void parseViewOptionsIntoMap(map base, string route, string path) {
		let parse = map (string file) => {};

		native javascript {
			parse = require(this.server.websomRoot + "/webpack/view-loader/parser.js");
		}

		let parsed = parse(FileSystem.readSync(path, "utf8"));

		if ("config" in parsed) {
			let map info = Websom.Json.parse("{" + parsed["info"]["block"] + "}");
			let string name = info["name"];
			let parsedConfig = Websom.Json.parse("{" + parsed["config"]["block"] + "}");
			base[route + "." + name] = parsedConfig["options"];
			let opts = base[route + "." + name];

			for (let string k in opts) {
				opts[k]["publicView"] = true;
			}
		}
	}

	map cacheOptionsFromPackage(string ptype, string pid, map baseConfig, string root) {
		let map baseOptions = new map();

		let parse = map (string file) => {};

		native javascript {
			parse = require(this.server.websomRoot + "/webpack/view-loader/parser.js");
		}

		let parseViews = void (string route, <string>array views) => {
			for (let int i in views) {
				let parsed = parse(FileSystem.readSync(views[i], "utf8"));

				if ("config" in parsed) {
					let map info = Websom.Json.parse("{" + parsed["info"]["block"] + "}");
					let string name = info["name"];
					let parsedConfig = Websom.Json.parse("{" + parsed["config"]["block"] + "}");
					baseOptions[route + "." + name] = parsedConfig["options"];
					let opts = baseOptions[route + "." + name];

					for (let string k in opts) {
						opts[k]["publicView"] = true;
					}
				}
			}
		};

		if ("config" in baseConfig) {
			let merge = baseConfig["config"];
			
			for (let string namespace in merge) {
				if (namespace == "root") {
					baseOptions[ptype + "." + pid] = merge[namespace];
				}else{
					baseOptions[namespace] = merge[namespace];
				}
			}
		}

		let views = new <string>array();
		
		if ("resources" in baseConfig) {
			let <map>array resources = baseConfig["resources"];

			for (let int i in resources) {
				let map res = resources[i];

				if (res["type"] == "view") {
					views.push(root + "/" + res["path"]);
				}else if (("type" in res) == false) {
					if (FileSystem.exists(root + "/" + res["path"])) {
						let dir = FileSystem.readDirSync(root + "/" + res["path"]);

						for (let int di in dir) {
							let string file = dir[di];
							let splits = file.split(".");
							let ext = splits[splits.length - 1];

							if (ext == "view") {
								views.push(root + "/" + res["path"] + "/" + file);
							}
						}
					}
				}
			}
		}

		parseViews(ptype + "." + pid, views);

		return baseOptions;
	}

	map cacheOptions() {
		let cache = new map();

		for (let int i in this.server.module.modules) {
			let mod = this.server.module.modules[i];
			cache[mod.id] = this.cacheOptionsFromPackage("module", mod.id.toLower(), mod.baseConfig, mod.root);
		}

		for (let int i in this.server.theme.themes) {
			let theme = this.server.theme.themes[i];
			let key = theme.key;
			if (key.length == 0)
				key = "theme";

			cache[key] = this.cacheOptionsFromPackage("theme", key.toLower(), theme.config, theme.root);
		}

		this.parseViewOptionsIntoMap(cache["theme"], "theme.theme", this.server.websomRoot + "/webpack/app.view");

		let cacheFile = this.server.config.configOverrides + "/optionsCache.json";
		FileSystem.writeSync(cacheFile, Websom.Json.encode(cache));

		return cache;
	}

	void fillDefaults(map configs) {
		for (let string owner in configs) {
			let routes = configs[owner];
			
			for (let string route in routes) {
				let options = routes[route];

				for (let string option in options) {
					let map opt = options[option];
					this.optionDefaults[route + "." + option] = opt["default"];
					let public = false;

					if ("public" in opt)
						public = opt["public"];

					if ("publicView" in opt)
						public = true;

					let arr = new <string>array();
					arr.push(route);
					arr.push(option);

					if (opt["publicView"])
						arr.push("view");
					else
						arr.push("package");

					if (public)
						this.publicDefaults.push(arr);
				}
			}
		}
	}

	map getCustomization() {
		let path = this.server.config.configOverrides + "/customization.json";

		if (FileSystem.exists(path)) {
			return Websom.Json.parse(FileSystem.readSync(path, "utf8"));
		}else{
			return new map();
		}
	}

	map getColorProfiles() {
		let custom = this.getCustomization();

		if ("color" in custom) {
			return custom["color"];
		}else{
			return new map();
		}
	}

	map getNavigationConfig() {
		let custom = this.getCustomization();

		if ("navigation" in custom) {
			return custom["navigation"];
		}else{
			return new map();
		}
	}

	map getConfiguredOptions() {
		let map data = this.optionValuesFromDatabase.clone();

		for (let string k in this.optionValues) {
			data[k] = this.optionValues[k];
		}

		return data;
	}

	map computeClientData(Websom.Request req) {
		let mp = new map();
		if (this.publicValueCache == false) {
			for (let int i in this.publicDefaults) {
				let p = this.publicDefaults[i];
				if (p[2] == "package") {
					this.publicValues[p[0] + "." + p[1]] = this.getPrimitive(p[0], p[1]);
				}else{
					let val = this.getPrimitiveNonDefault(p[0], p[1]);
					
					if (val != null) {
						this.publicValues[p[0] + "." + p[1]] = val;
					}
				}
			}

			this.publicValueCache = true;
		}

		mp["config"] = this.publicValues;

		mp["routes"] = new map();
		mp["endpoints"] = this.server.api.gatherEndpoints(req);
		mp["navigation"] = new map();

		let adapters = new map();
		
		for (let int i in this.server.adaptation.interfaces) {
			let interface = this.server.adaptation.interfaces[i];

			if (interface.adapter != null and interface.exposeToClient) {
				adapters[interface.name] = interface.adapter.adapterKey;
			}
		}

		mp["adapters"] = adapters;

		let data = this.getNavigationConfig();

		if ("navbar" in data)
			mp["navigation"]["config"] = data["navbar"];
		else
			mp["navigation"]["config"] = new map();

		mp["navigation"]["navbar"] = new <map>array();

		return mp;
	}
}