namespace CoreModule {}

#script Carbon
	print("Core module");
#end script

class CoreModule.Module inherits Websom.Module {
	Websom.Adapters.Database.Collection test = null;
	Websom.Adapters.Database.Collection groups = null;
	Websom.Adapters.Database.Collection confirmations = null;

	public Websom.Permission commentEdit = null;
	public Websom.Permission commentCreate = null;
	public Websom.Permission commentRead = null;

	Websom.Permission groupRead = null;
	Websom.Permission groupCreate = null;
	Websom.Permission groupEdit = null;

	override void permissions() {
		this.commentEdit = new Websom.Permission("Comment.Edit");
		this.commentEdit.description = "Allows users to edit any comment";
		
		this.commentCreate = new Websom.Permission("Comment.Create");
		this.commentCreate.description = "Allows users to create a comment";

		this.commentRead = new Websom.Permission("Comment.Read");
		this.commentRead.description = "Read permissions on any comment anywhere";
		this.commentRead.public = true;
		
		this.registerPermission(this.commentEdit);
		this.registerPermission(this.commentCreate);
		this.registerPermission(this.commentRead);

		this.groupRead = this.registerPermission("Group.Read")
			.setDescription("Allows users to read permission group information.");
		
		this.groupCreate = this.registerPermission("Group.Create")
			.setDescription("Allows users to create permission groups. WARNING This is an admin level permission.");
		
		this.groupEdit = this.registerPermission("Group.Edit")
			.setDescription("Allows users to edit permission groups. WARNING This is an admin level permission.");
	}

	override void collections() {
		let db = this.server.database.central;

		this.confirmations = db.collection("confirmations");
		let confirmationSchema = this.confirmations.schema()
			.field("secret", "string")
			.field("key", "string")
			.field("ip", "string")
			.field("created", "time")
			.field("storage", "string")
			.field("expires", "time")
			.field("confirmed", "boolean")
			.field("service", "string")
			.field("method", "string")
			.field("to", "string");

		this.registerCollection(this.confirmations);

		this.groups = db.collection("groups");

		Websom.Group.applySchema(this.groups);

		this.registerCollection(this.groups);

		this.server.api.interface(this.groups, "/groups")
			.route("/create")
				.auth(this.groupCreate)
				.executes("insert")
					.write("name")
					.write("description")
					.write("permissions")
					.write("rules")
					.write("public")
					.write("user")
					.setComputed("created", uint64 (Websom.Request req) => { return Websom.Time.now(); })
			.route("/find")
				.auth(this.groupRead)
				.executes("select")
					.read("*")
					.filter("default")
						.order("created", "dsc")
			.route("/read")
				.auth(this.groupRead)
				.executes("select")
					.read("*")
					.filter("default")
						.field("id", "==");

		this.test = db.collection("test");

		let schema = this.test.schema()
			.field("name", "string")
			.field("balance", "float")
			.calc("averageBalance", new Websom.Calculators.Average("balance"))
			.index()
				.field("name", "==")
				.field("balance", "dsc");
			
		this.registerCollection(this.test);
		let int x = Websom.Time.now();
		this.test.insert()
			.set("name", "Hello")
			.set("balance", Math.sin(x))
			.run();

		let res = this.test.where("name", "==", "Hello").get();

		this.server.api.interface(this.test, "/testing")
			.route("/create")
				.auth(this.commentCreate)
				.executes("insert")
					.write("name")
						.limit(3, 256)
					.set("balance", 0)
			.route("/edit")
				.auth(this.commentEdit)
				.executes("update")
					.write("name")
					.filter("default")
						.field("id", "==")
			.route("/find")
				.auth(this.commentRead)
				.executes("select")
					.read("name")
					.read("balance")
					.filter("default")
						.field("name", "==")
						.force("balance", "<", 100)
						.order("balance", "dsc");
	}

	override void registerWithServer() {
		let adapter = new CoreModule.Confirmation(this.server);
		adapter.module = this;
		this.server.confirmation.confirmation = adapter;
	}

	override Websom.Status start() {
		this.server.api.route("/custom/endpoint", void (Websom.Request req) => {
			req.end("Hello");
		});
	}
}

#include "adapters/loki"
#include "adapters/sendGrid"
#include "adapters/confirmation"